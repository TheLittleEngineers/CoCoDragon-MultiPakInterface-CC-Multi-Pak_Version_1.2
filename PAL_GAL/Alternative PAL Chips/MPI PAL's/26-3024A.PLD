Name     26-3024a ;
PartNo   RAT-3024a ;
Date     12/12/2008 ;
Revision 01 ;
Designer "The R.A.T." ;
Company  Binary Systems ;
Assembly 26-3024 M.P.I. Original PAL, Second Version ;
Location U6 ;
Device   g16v8 ;

/* ***************************************** INPUT PINS **********************************************/
PIN 1 = !FFXX;        /* The M.P.I. Circuitry Drives This Pin LOW For Any Address Between $FF00-$FFFF*/ 
PIN [2..9] = [A7..0]; /* CPU Address Lines A7 - A0                                                   */
PIN 11 = RW;          /* READ = 1, WRITE = 0                                                         */ 
PIN 12 = E;           /* System E-Clock (ENABLE a.k.a. PHASE 2)                                      */ 
PIN 13 = Q;           /* System Q-Clock (QUADRATURE a.k.a. PHASE 1)                                  */ 
PIN 18 = !CTS;        /* Cartridge Select Signal. Used to enable Program Pak ROM Software. Read Only.*/
PIN 19 = !SLENB;      /* Input To Disable Device Selection.                                          */                    
/* ***************************************** OUTPUT PINS *********************************************/
PIN 14 = !DBEN;       /* Active LOW - Enables The Multi-Pak Interface Data Buffer                    */
PIN 15 = !IOR;        /* Active LOW - Allows The Contents Of The Slot Select Latch To Be Read        */ 
PIN 16 = !IOW;        /* Active LOW - Allows Writing Data Into The Slot Select Latch                 */
/* ***************************** LOGIC EQUATIONS & VARIABLE DEFINITIONS ******************************/
FIELD ADDRESS = [A7..0];
LATCH = ADDRESS:[7F]; /* Original M.P.I. Latch Addressed At $FF7F And $FF9F, This Version $FF7F Only.*/
IOR = LATCH & FFXX & E & RW;                    /* Active LOW on $FF7F and E=1 and RW=1              */
IOW = LATCH & FFXX & E & !RW & !Q;              /* Active LOW on $FF7F and E=1 and RW=0 and Q=0      */
DBEN = (ADDRESS:[00..BF] & FFXX) # SLENB # CTS; /* Active LOW on $FF00-$FFBF or SLENB or CTS         */
/* ***************************************************************************************************/
/* *This is a "clone" of the original PAL chip from the original Large White Multi-Pak Interface     */
/* *Radio Shack #26-3024. The slot select latch is mapped at $FF7F Only. The M.P.I. Data Buffer      */
/* *is enabled on all Addresses between $FF00 and $FFBF and is also active on either, or both the    */
/* *!SLENB and !CTS signals. This "clone" is 100% designed by and Copyright of:                      */
/* *Robert "The R.A.T." Allen Turner, (c) 1994 - 2008, All Rights Reserved.                          */
/* *CC BY-SA 4.0 License, 04/11/2024                                                                 */
/* ***************************************************************************************************/
